
plugins {
    id 'net.onebeastchris.geyserpacksync.java-conventions'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
    id "com.modrinth.minotaur" version "2.+"
}

dependencies {
    implementation project(':common')
    compileOnly("com.velocitypowered:velocity-api:3.3.0-SNAPSHOT")
    compileOnly("org.geysermc.geyser:api:2.2.0-SNAPSHOT")
    annotationProcessor("com.velocitypowered:velocity-api:3.3.0-SNAPSHOT")

    compileOnly("io.netty:netty-all:4.1.104.Final")
}

jar {
    dependsOn ':common:jar'
    archiveClassifier.set('unshaded')
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
}

shadowJar {
    archiveBaseName.set('geyserpacksync-velocity')
    archiveClassifier.set('')

    relocate('com.fasterxml.jackson', 'net.onebeastchris.geyserpacksync.jackson')
    relocate('org.yaml', 'net.onebeastchris.geyserpacksync.yaml')
}

modrinth {
    token.set(System.getenv("MODRINTH_TOKEN"))
    projectId.set("geyserpacksync")
    versionName.set(project.version as String)
    versionNumber.set(project.version as String + "-b" + System.getenv("GITHUB_RUN_NUMBER"))
    versionType.set("release")
    uploadFile.set(shadowJar)
    gameVersions = ["1.20.3", "1.20.4"]
    loaders = ["velocity"]

    syncBodyFrom.set(rootProject.file("README.md").text)
    changelog.set(rootProject.file("CHANGELOG.md").text)

    failSilently.set(true)
}